#####
= element QMembrane. The parent class of inner and outer membrane
\symmetries all

== Rules: OM management (mostly growing)
given @ isa OuterMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane
given n { return !is(ew(_cursn), InnerMembrane); }

given e { return true; // Allow dead sites }
vote  e isa Empty   # Count empty sites
check e { return _nvotes*3u >= _nsites*2u; } 

vote  f isa QContent # Count up content
check f { return random_oddsOf(_nvotes,3); }

# Self-stabilization rules

  i_       .@
  i@   ->  ..    # Square off (complete outer membrane)

  nnn      ...
  n@n  ->  ._.   # Die off (eliminate isolated outer membrane)
  nnn      ...

  iii      ...
  i@i  ->  .i.   # Turn in (eliminate surrounded outer membrane)
  iii      ...

  # Growth rules

 ee_oq     ..@..
 eeo@i ->  ...i.     # Run out
 eeoii     .....

    eo       ..
    _oi      o..
    _@i  ->  @i.       # Break out
    _oi      o..
    eo       ..
    
    qii     ...
    o@i  -> .i.        # Punch out
    qii     ...

vote r { return is(ew(_cursn), Empty) || is(ew(_cursn), OuterMembrane) ? 1 : 0; } 

    roqf          o...
    o@if    ->    .i..   # Cave out
    qiif          ....
    ffff          ....

# OuterMembrane topology changing rules

   fffff          .....
  iiiiiii        .......
 oooo@oooo  ->  ....i....  # Fusion
  iiiiiii        .......
   fffff          .....

#
#
# Missing 'tail shrinking' rule
# let and $o. sytnax not supported yet...
#
#

== Rules: IM management (shrink and die)
given @ isa InnerMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane
given n { return !is(ew(_cursn), OuterMembrane); }

vote  e isa QContent
check e { return _nvotes == 0u; // Say no QContent }

given f { return true;   // Allow dead sites }
vote  f { return !is(ew(_cursn), Empty) ? 1 : 0; }
check f { return random_oddsOf(3u*_nvotes+1u,10u); }

# Self-stabilization rules

  o_       .@
  o@   ->  ..       # Square in

  nnn      ...
  n@n  ->  ._.      # Die in
  nnn      ...

 _____     .....
 __oo_     .o...
  o@o  ->   ...     # Put your cap on

  ooo      ...
  o@o  ->  .o.      # Turn out
  ooo      ...

given z { return !ew_isLive(_cursn); }

  o      .
  @  ->  o        # Scrub universe walls
  z      .

# Shrink rules
 ee_iqff     ..@....
 eei@off ->  ...o...     # Run in
 eeiooff     .......

   ei        ..
   _iof      i...
   _@of  ->  @o..        # Break in
   _iof      i...
   ei        ..

      ff       ..
    qoof     ....
    i@of  -> .o..        # Punch in
    qoof     ....
      ff       ..

vote r { return is(ew(_cursn), Empty) || is(ew(_cursn), InnerMembrane) ? 1 : 0; } 

   riqf          i...
   i@of    ->    .o..     # Cave in
   qoof          ....
   ffff          ....

# InnerMembrane topology changing rules

    _____          .....
   ooooooo        .......
  iiii@iiii  ->  ....o....  # Fission
   ooooooo        .......
    _____          .....

## BOGUS RULES FOR CRASH TESTING
#    _____          .....
#   oooooio        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooooo        .......
#    _____          .....
#
#    _____          .....
#   ooooooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooioooo        .......
#    _____          .....
#
#    _____          .....
#   ooooooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooooo        .......
#    _____          .....
#
#    _____          .....
#   ooioooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooooo        .......
#    _____          .....
#
#    _____          .....
#   ioooooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooooo        .......
#    _____          .....
#
#    _____          .....
#   ooooooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooioo        .......
#    _____          .....
#
#    _____          .....
#   iooiooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   ooooooo        .......
#    _____          .....
#
#    _____          .....
#   ooooooo        .......
#  iiii@iiii  ->  ....o....  # Fission
#   oooooii        .......
#    _____          .....
#

# Postrules: Last ditch hold missing @ -> .
# postrules aren't supported yet
#